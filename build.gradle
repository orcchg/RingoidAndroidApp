apply from: 'buildsystem/dependencies.gradle'

static String getAppId(boolean isStaging) {
    if (isStaging) return 'com.ringoid.app.staging'
    return 'com.ringoid.app'
}

static String getApiUrl(boolean isStaging) {
    if (isStaging) return '"https://stage.ringoidapp.com"'
    return '"https://prod.ringoidapp.com"'
}

static String getVersionName(boolean isStaging, int major, int middle, int minor, int build) {
    String version = "$major.$middle.$minor.$build"
    if (isStaging) return "$version-staging"
    return version
}

ext {
    isStaging = true
    apiUrl = getApiUrl(isStaging)
    host = '"ringoid.com"'
    authority = '"https://ringoid.com"'
    appnav = '"appnav://ringoid.com"'
}

buildscript {
    apply from: 'buildsystem/version.gradle'

    repositories {
        google()
        jcenter()
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath "com.android.tools.build:gradle:${buildGradleVersion}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}"
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:${kotlinVersion}"
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url 'https://jitpack.io' }
    }

    ext {
        majorVersion = 1
        middleVersion = 0
        minorVersion = 0
        buildVersion = 86

        androidApplicationId = getAppId(isStaging)
        androidVersionCode = buildVersion
        androidVersionName = getVersionName(isStaging, majorVersion, middleVersion, minorVersion, buildVersion)
        testInstrumentationRunner = "android.support.test.runner.AndroidJUnitRunner"
        testApplicationId = 'com.ringoid.app.test'
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
