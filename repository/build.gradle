rootProject.repositoryPlugins.each {
    apply plugin: it
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.androidVersionCode
        versionName rootProject.ext.androidVersionName
        testInstrumentationRunner rootProject.ext.testInstrumentationRunner
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    kapt {
        arguments {
            arg("room.schemaLocation", "$projectDir/schemas".toString())
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    sourceSets {
        main {
            java.srcDirs += 'src/main/kotlin'
        }
        androidTest {
            assets.srcDirs += files("$projectDir/schemas".toString())
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    def repositoryDependencies = rootProject.ext.repositoryDependencies
    def testRepositoryDependencies = rootProject.ext.testRepositoryDependencies

    repositoryDependencies.each {
        add(it.configuration, it.dependency, it.options)
    }
    testRepositoryDependencies.each {
        add(it.configuration, it.dependency, it.options)
    }
}
